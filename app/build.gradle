apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
	
    defaultConfig {
        applicationId "com.dork.app.react"
        minSdkVersion 18
        targetSdkVersion 25
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionCode 1
        versionName "0.0.1"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
	lintOptions {
        warning 'InvalidPackage'
        disable 'MissingTranslation'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_7
        sourceCompatibility JavaVersion.VERSION_1_7
    }
}

// Additional repositories
repositories {
    maven {
        url "https://jitpack.io"
    }
    jcenter()
    mavenCentral()
}

dependencies {
	// Sub-Projects
    compile(project(':swagger')) {
        exclude module: "httpmime"
        exclude module: "httpcore"
        exclude module: "httpclient"
    }

	// Jars
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android standard libs
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'

    // Open Street Map & Geo
    compile 'org.osmdroid:osmdroid-android:5.6.3'
    compile 'com.github.MKergall:osmbonuspack:6.2'
    compile 'ch.hsr:geohash:1.3.0'

    // More libraries
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.android.support:multidex:1.0.1'

    // authentication libraries
    compile 'com.google.android.gms:play-services:10.2.0'
    compile 'com.google.android.gms:play-services-auth:10.2.0'

	// Testing-only dependencies
    testCompile 'junit:junit:4.12'
	androidTestCompile 'com.android.support:support-annotations:25.1.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    // Hamcrest library
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    // UI testing with Espresso
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // UI testing with UI Automator
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}

apply plugin: 'com.google.gms.google-services'

final String RAW_DIR = "app/src/main/res/raw"
final String[] ROOT_TO_RAW_COPYFILES = ["README.md", "LICENSE.md", "CHANGELOG.md", "CONTRIBUTORS.txt"]

// Called before building
task copyRepoFiles(type: Copy) {

    // Copy files over to raw dir
    from rootProject.files(ROOT_TO_RAW_COPYFILES)
    into RAW_DIR.replaceFirst("app/", "")
    rename { String fileName ->
        fileName.replace(fileName, fileName.toLowerCase())
    }

    // Filter Contributors file
    from(rootProject.file("CONTRIBUTORS.txt")) {
        into '.'    // Target already changed to 'src/main/res/raw'
        rename { String fileName ->
            fileName.replace(fileName, fileName.toLowerCase())
        }
        filter { line ->
            (line.toString().matches("..l>>.*") || line.toString().startsWith("## 99l CONTRIBUTORS")) ? null : line.toString().trim().replaceAll(" \\(.*\\)", "")
        }
    }
}

tasks.copyRepoFiles.execute()